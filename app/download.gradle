//  ----------------------------------------------------------------------------
//    The confidential and proprietary information contained in this file may
//    only be used by a person authorized under and to the extent permitted
//    by a subsisting licensing agreement from ARM Limited or its affiliates.
//
//           (C)COPYRIGHT 2020 ARM Limited or its affiliates.
//               ALL RIGHTS RESERVED
//
//    This entire notice must be reproduced on all copies of this file
//    and copies of this file may only be made by a person if such person is
//    permitted to do so under the terms of a subsisting license agreement
//    from ARM Limited or its affiliates.
//  ----------------------------------------------------------------------------

def targetFolder = "libs"
def downloadUrlForSDK = "http://jenkins-mobile.pelion.arm.com/downloads/sdk/pmtsdk.zip"
def localCacheForSDK = "build/intermediates/pmtsdk.zip"

task downloadSDK(type: DownloadUrlTask) {
    doFirst {
        println "Downloading ${downloadUrlForSDK}"
    }
    sourceUrl = "${downloadUrlForSDK}"
    target = file("${localCacheForSDK}")
}

task unzipSDK(type: Copy, dependsOn: 'downloadSDK') {
    doFirst {
        println "Unzipping ${localCacheForSDK}"
    }
    from zipTree("${localCacheForSDK}")
    into "${targetFolder}"
}

class DownloadUrlTask extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}

preBuild.dependsOn unzipSDK